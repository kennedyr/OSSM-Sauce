extends Node

var server: WebSocketServer

var port: int = 8008
var host: String = "0.0.0.0"

var server_started: bool
var ossm_connected: bool
var ping_timer: Timer

var ping_id:int = 0

func _ready():
	server = WebSocketServer.new()
	server.client_connected.connect(_on_client_connected)
	server.client_disconnected.connect(_on_client_disconnected)
	server.message_received.connect(_on_message_received)
	server.data_received.connect(_on_data_received)
	server.server_error.connect(_on_server_error)
	ping_timer = Timer.new()
	ping_timer.wait_time = 3.0
	ping_timer.timeout.connect(func(): server.broadcast_ping())
	add_child(ping_timer)


func start_server():
	server_started = server.start(port, host)
	if server_started:
		print("WebSocket server started successfully on port %d" % port)
		ping_timer.start()
	else:
		printerr("Failed to start WebSocket server on port %d" % port)
	
	update_server_status()


# Process signals from the main thread to listen for incoming messages
func _process(delta: float) -> void:
	if server and server.is_listening():
		server.process()


func update_server_status():
	if server.is_listening():
		print("Server Status: Running on %s:%d" % [host, port])
		%WiFi.self_modulate = Color.WHITE
		%WiFi.show()
		server_started = true
	else:
		print("Server Status: Stopped")
		%WiFi.hide()
		server_started = false
		ossm_connected = false
	update_client_count()


func update_client_count():
	print("Connected Clients: %d" % server.get_client_count())


func _on_client_connected(client_id):
	print("Client connected: #%d" % client_id)
	update_client_count()


func _on_client_disconnected(client_id, code):
	print("Client disconnected: #%d (code: %d)" % [client_id, code])
	update_client_count()
	if server.get_client_count() == 0:
		_on_client_disconnected_cleanup()


func ping(client_id):
	ping_id = Time.get_ticks_msec()
	print("Sending ping to client %d: %d" % [client_id, ping_id])
	server.send_text(client_id, "PING%d" % [ping_id])


func _on_message_received(client_id, message):
	print("Text message from client %d: %s" % [client_id, message])
	if message.begins_with("PING"):
		server.send_text(client_id, message.replace("PING", "PONG"))
	elif message.begins_with("PONG"):
		if message.contains(ping_id):
			var pong_id = Time.get_ticks_msec()
			print("Latency in milliseconds %d" % [pong_id - ping_id])


func _on_data_received(client_id, data):
	if data[0] == OSSM.Command.RESPONSE:
		match data[1]:
			OSSM.Command.CONNECTION:
				%WiFi.self_modulate = Color.SEA_GREEN
				%WiFi.show()
				
				# Stop fast processes for safety
				%VibrationControls.set_process(false)
				%PositionControls.set_physics_process(false)
				
				# Release any held click/press input for safety
				var release_event = InputEventMouseButton.new()
				release_event.button_index = MOUSE_BUTTON_LEFT
				release_event.pressed = false
				Input.parse_input_event(release_event)
				
				ossm_connected = true
				ping(client_id)
				owner.apply_device_settings()
				
				# Reset and home to base by reselecting mode
				%Menu._on_mode_selected(%Menu/Main/Mode.selected)
			
			OSSM.Command.HOMING:
				%CircleSelection.hide()
				%CircleSelection.homing_lock = false
				%ActionPanel.disable_buttons(false)
				var displays = [
					%PathDisplay,
					%PositionControls,
					%LoopControls,
					%VibrationControls,
					%BridgeControls,
					%ActionPanel,
					%VideoPlayer,
					%Settings,
					%AddFile,
					%Menu]
				for node in displays:
					node.modulate.a = 1
				Global.emit_signal("homing_complete")
				if AppMode.active == AppMode.MOVE:
					if Global.active_path_index != null and global.frame == 0:
						%CircleSelection.show_play()


func _on_client_disconnected_cleanup():
	ossm_connected = false
	%WiFi.self_modulate = Color.WHITE
	%ActionPanel._on_pause_button_pressed()
	# Unblock any awaiting homing
	%CircleSelection.hide()
	%CircleSelection.homing_lock = false
	%ActionPanel.disable_buttons(false)
	var display = [
			%PathDisplay,
			%PositionControls,
			%LoopControls,
			%VibrationControls,
			%ActionPanel,
			%Menu]
	for node in display:
		node.modulate.a = 1
	owner.emit_signal("homing_complete")


func _on_server_error(error):
	printerr("Server error: %s" % error)


func _exit_tree():
	if server and server.is_listening():
		server.stop()
